/*
 * Intel Edison
 *
 * This adds MMC SPI slot to the SPI host controller available on
 * Intel Edison header.
 *
 * In Linux you need to set CONFIG_MMC_SPI=y (or m) to be able to use
 * this device.
 *
 * Include this file from an ASL providing SSDT DefinitionBlock.
 */

External (\_SB.PCI0.SPI5, DeviceObj)

Scope (\_SB.PCI0.SPI5)
{
    Device (MMCS)
    {
        Name (_HID, "PRP0001")
        Name (_DDN, "MMC SPI slot")
        Name (_CRS, ResourceTemplate () {
            SpiSerialBus (
                1,                      // Chip select
                PolarityLow,            // Chip select is active low
                FourWireMode,           // Full duplex
                8,                      // Bits per word is 8 (byte)
                ControllerInitiated,    // Don't care
                10000000,               // 10 MHz
                ClockPolarityLow,       // SPI mode 0
                ClockPhaseFirst,        // SPI mode 0
                "\\_SB.PCI0.SPI5",      // SPI host controller
                0                       // Must be 0
            )
        })

        /*
         * See Documentation/devicetree/bindings/mmc/mmc-spi-slot.txt
         * for more information about these bindings.
         */
        Name (_DSD, Package () {
            ToUUID("daffd814-6eba-4d8c-8a91-bc9bbf4aa301"),
            Package () {
                Package () { "compatible", Package () { "mmc-spi-slot" } },
            }
        })

        #include "gpioexp-dep.asli"
    }
}
