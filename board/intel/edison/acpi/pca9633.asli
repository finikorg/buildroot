/*
 * Intel Edison
 *
 * This adds NXP PCA9633 LED to the I2C6 bus available
 * on Intel Edison header.
 *
 * In Linux you need to set CONFIG_LEDS_PCA963X=y (or m)
 * to be able to use this device.
 */
External (\_SB.PCI0.I2C6, DeviceObj)

Scope (\_SB.PCI0.I2C6)
{
    Device (LEDS) {
        Name (_HID, "PRP0001")
        Name (_DDN, "NXP PCA9633 LED")
        Name (_CRS, ResourceTemplate () {
            I2cSerialBus (
                0x62,                   // Bus address
                ControllerInitiated,    // Don't care
                400000,                 // Fast mode (400 kHz)
                AddressingMode7Bit,     // 7-bit addressing
                "\\_SB.PCI0.I2C6",      // I2C host controller
                0                       // Must be 0
            )
        })

        /*
         * See Documentation/devicetree/bindings/leds/common.txt
         * for more information about these bindings.
         */
        Name (_DSD, Package () {
            ToUUID("daffd814-6eba-4d8c-8a91-bc9bbf4aa301"),
            Package () {
                Package () { "compatible", Package() { "nxp,pca9633" } },
            },
            ToUUID("dbb8e3e6-5886-4ba6-8795-1319f52a966b"),
            Package () {
                Package () { "led-0", "LED0" },
                Package () { "led-1", "LED1" },
                Package () { "led-2", "LED2" },
                Package () { "led-3", "LED3" },
            }
        })

        Name (LED0, Package () {
            ToUUID("daffd814-6eba-4d8c-8a91-bc9bbf4aa301"),
            Package () {
                Package () { "reg", 0 },
                Package () { "label", "blue" },
            }
        })

        Name (LED1, Package () {
            ToUUID("daffd814-6eba-4d8c-8a91-bc9bbf4aa301"),
            Package () {
                Package () { "reg", 1 },
                Package () { "label", "green" },
            }
        })

        Name (LED2, Package () {
            ToUUID("daffd814-6eba-4d8c-8a91-bc9bbf4aa301"),
            Package () {
                Package () { "reg", 2 },
                Package () { "label", "red" },
            }
        })

        Name (LED3, Package () {
            ToUUID("daffd814-6eba-4d8c-8a91-bc9bbf4aa301"),
            Package () {
                Package () { "reg", 3 },
                Package () { "label", "not connected" },
            }
        })
    }
}
