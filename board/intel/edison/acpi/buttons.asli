/*
 * Intel Edison
 *
 * This adds GPIO buttons device found on the SparkFun OLED block
 * (DEV-13035).
 *
 * In Linux you need to set CONFIG_KEYBOARD_GPIO=y (or m) to be able to use
 * this device.
 *
 * Copyright (C) 2019, Intel Corporation
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
 * THE SOFTWARE.
 *
 * Include this file from an ASL providing SSDT DefinitionBlock.
 */

External (\_SB.PCI0, DeviceObj)

Scope (\_SB.PCI0)
{
    Device (BTNS)
    {
        Name (_HID, "PRP0001")
        Name (_DDN, "GPIO buttons device")

        Name (_CRS, ResourceTemplate () {
            GpioIo(Exclusive, PullUp, 0, 0, IoRestrictionInputOnly,
                "\\_SB.PCI0.GPIO", 0, ResourceConsumer, , )
            {
                47,	// Up
                44,	// Down
                165,	// Left
                45,	// Right
                48,	// Center
            }
            GpioIo(Exclusive, PullUp, 0, 0, IoRestrictionInputOnly,
                "\\_SB.PCI0.GPIO", 0, ResourceConsumer, , )
            {
                49,	// Button A
                46,	// Button B
            }
        })

        Name (_DSD, Package () {
            ToUUID("daffd814-6eba-4d8c-8a91-bc9bbf4aa301"),
            Package () {
                Package () { "compatible", Package() { "gpio-keys" } },
                Package () { "label", "Joystick" },
                Package () { "autorepeat", 1 },
            },
            ToUUID("dbb8e3e6-5886-4ba6-8795-1319f52a966b"),
            Package () {
                Package () {"joystick-up", "JOYU"},
                Package () {"joystick-down", "JOYD"},
                Package () {"joystick-left", "JOYL"},
                Package () {"joystick-right", "JOYR"},
                Package () {"joystick-center", "JOYC"},
                Package () {"button-A", "BTNA"},
                Package () {"button-B", "BTNB"},
            }
        })

        /*
         * For more information about these bindings see:
         * Documentation/devicetree/bindings/input/gpio-keys.txt
         * and Documentation/acpi/gpio-properties.txt.
         */
        Name (JOYU, Package () {
            ToUUID("daffd814-6eba-4d8c-8a91-bc9bbf4aa301"),
            Package () {
                Package () {"label", "Joystick Up"},
                Package () {"linux,code", 103},
                Package () {"linux,input-type", 1},
                Package () {"gpios", Package () {^BTNS, 0, 0, 1}},
            }
        })

        Name (JOYD, Package () {
            ToUUID("daffd814-6eba-4d8c-8a91-bc9bbf4aa301"),
            Package () {
                Package () {"label", "Joystick Down"},
                Package () {"linux,code", 108},
                Package () {"linux,input-type", 1},
                Package () {"gpios", Package (4) {^BTNS, 0, 1, 1}},
            }
        })

        Name (JOYL, Package () {
            ToUUID("daffd814-6eba-4d8c-8a91-bc9bbf4aa301"),
            Package () {
                Package () {"label", "Joystick Left"},
                Package () {"linux,code", 105},
                Package () {"linux,input-type", 1},
                Package () {"gpios", Package () {^BTNS, 0, 2, 1}},
            }
        })

        Name (JOYR, Package () {
            ToUUID("daffd814-6eba-4d8c-8a91-bc9bbf4aa301"),
            Package () {
                Package () {"label", "Joystick Right"},
                Package () {"linux,code", 106},
                Package () {"linux,input-type", 1},
                Package () {"gpios", Package () {^BTNS, 0, 3, 1}},
            }
        })

        Name (JOYC, Package () {
            ToUUID("daffd814-6eba-4d8c-8a91-bc9bbf4aa301"),
            Package () {
                Package () {"label", "Joystick Center"},
                Package () {"linux,code", 288},
                Package () {"linux,input-type", 1},
                Package () {"gpios", Package () {^BTNS, 0, 4, 1}},
            }
        })

        Name (BTNA, Package () {
            ToUUID("daffd814-6eba-4d8c-8a91-bc9bbf4aa301"),
            Package () {
                Package () {"label", "Button A"},
                Package () {"linux,code", 104},
                Package () {"linux,input-type", 1},
                Package () {"gpios", Package () {^BTNS, 1, 0, 1}},
            }
        })

        Name (BTNB, Package () {
            ToUUID("daffd814-6eba-4d8c-8a91-bc9bbf4aa301"),
            Package () {
                Package () {"label", "Button B"},
                Package () {"linux,code", 109},
                Package () {"linux,input-type", 1},
                Package () {"gpios", Package () {^BTNS, 1, 1, 1}},
            }
        })
    }
}
